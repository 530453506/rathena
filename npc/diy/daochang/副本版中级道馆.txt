function	script	中级道馆	{
	mes "[中级道场挑战]";
	mes "欢迎您参加中级道馆";
	mes "此道馆为副本版,可以提供多个队伍进入";
	mes "^0000FF[组队限制] -12人版^000000";
	mes "进入副本后有时间限定,请玩家们注意时间";
	mes "队长消耗^FF0000「RoZ」^000000 "+$@Dc_In_Zeny_2+"Z"; 
	next;
	menu "道场报名",-,"看看奖励",kjl_2;
	mes "[道场挑战]";
	mes "进入副本队长需要：";
	// 循环显示进入要求
	for (@a=0;@a<getarraysize($@zjdc_item_id);@a++)
	{
		mes ""+getitemname($@zjdc_item_id[@a])+"x"+$@zjdc_item_num[@a];
	}
	mes "金钱 ^FF0000"+$@Dc_In_Zeny_2+"^000000";
	mes "-------------------------";
	// 判断是否有创建副本的资格
	if ( !getcharid(1) || getcharid(0) != getpartyleader(getcharid(1),2) )
	{
		mes "对不起，你没有队伍，或者 你不是队长。";
		close;
	}
	// 取得队伍成员信息组
	getpartymember(getcharid(1),2);
	set .@partymembercount,$@partymembercount;
	copyarray .@partymemberaid[0],$@partymemberaid[0],.@partymembercount;
	// 循环判断队伍中的成员是否符合要求
	// 显示出第一个不符合要求的成员
	for (.@i=0;.@i<.@partymembercount;.@i++)
	{
		if ( !isloggedin(.@partymemberaid[.@i]) || !attachrid(.@partymemberaid[.@i]) )
		{
			if ( !isloggedin(getpartyleader(getcharid(1),1)) || !attachrid(getpartyleader(getcharid(1),1)) )
					end;
			mes "队伍中有人不在线，无法报名。";
			close;
		}			
		if(BaseLevel < $@zjdc_level)
		{
			if ( !isloggedin(getpartyleader(getcharid(1),1)) || !attachrid(getpartyleader(getcharid(1),1)) )
				end;
			mes "队伍中的 ["+rid2name(.@partymemberaid[.@i])+"]";
			mes "等级还没到"+$@zjdc_level+".";
			close;
		}
		}
	// 脚本关联回到队长身上
	if ( !isloggedin(getpartyleader(getcharid(1),1)) || !attachrid(getpartyleader(getcharid(1),1)) )
		end;
	mes "你想登记进入道馆吗?";
	next;
	if ( select( "是的","不要" ) == 2 )
		close;
	mes "[道馆挑战]";
	// 判断队长身上的道具是否符合要求
	for (@b=0;@b<getarraysize($@zjdc_item_id);@b++)
	{
		set @online_num,callfunc("DC_Party_On",getcharid(1));
		if ( BaseLevel < $@zjdc_level )
		{
			mes "对不起！";
			mes "你的等级还没到"+$@zjdc_level+".";
			close;
		}
		if ( @online_num < $@zjdc_min )
		{
			mes "对不起！";
			mes "你的队伍成员必须在["+$@zjdc_min+"]人以上，";
			close;
		}
		if ( @online_num > $@zjdc_max )
		{
			mes "对不起！";
			mes "你的队伍成员超过["+$@zjdc_max+"]人，";
			close;
		}
		if ( Zeny < $@Dc_In_Zeny_2 )
		{
			mes "对不起！";
			mes "你的金钱不足"+$@Dc_In_Zeny_2+"Zeny，";
			close;
		}
		if ( countitem($@zjdc_item_id[@b]) < $@zjdc_item_num[@b] )
		{
			mes "对不起！";				
			mes "你的["+getitemname($@zjdc_item_id[@b])+"]不足["+$@zjdc_item_num[@b]+"]个。";
			close;
		}
	}
	mes "[道馆挑战]";
	mes "你确定要登记吗?";
	next;
	if ( select( "是的","不要" ) == 2 )
		close;
	mes "[道馆挑战]";
	mes "你的队伍已经成功登记了.";
	mes "我将打开一个入口，它会把你们传送到里面.";
	delitem 27019,1;
	set zeny,zeny-$@Dc_In_Zeny_2;
	close2;
	if(instance_id(1))
		instance_destroy(instance_id(1));
	set .@instance_id,instance_create("中级道场",getcharid(1));
	instance_attachmap("5@xg_vs2",.@instance_id);
	instance_set_timeout $@Dc_Time_All_2,$@Dc_Time_In_2,.@instance_id;
	instance_init .@instance_id;
	killmonsterall has_instance("5@xg_vs2");
	set 'Dc_Truns,0;
	set 'Dc_Party,0;	
	set 'Dc_timer,0;
	set 'Dc_Mob,0;
	enablenpc instance_npcname("中级_裁判",instance_id(1));
	disablenpc instance_npcname("中级_奖品发放员",instance_id(1));
	// 判断队员身上的道具是否符合要求
	getpartymember(getcharid(1),2);
	set .@partymembercount,$@partymembercount;
	copyarray .@partymemberaid[0],$@partymemberaid[0],.@partymembercount;		
	for (.@i=0;.@i<.@partymembercount;.@i++)
	{
		if ( !isloggedin(.@partymemberaid[.@i]) || !attachrid(.@partymemberaid[.@i]) )
				continue;
		set @Dc_Win_ZJ,1;
		warp has_instance("5@xg_vs2"),49,47;
	}
	end;
kjl_2:
	mes "----中级道馆奖励----";
	mes "固定奖励: 100W ROZ";
	for (set .@a,0;.@a<17;set .@a,.@a+1)
	mes "^0000FF["+getitemname($@Dc_Item_Id_2[.@a])+"]^000000 X ^FF0000"+$@Dc_Item_Num_2[.@a]+"^000000";
	close;
}

5@xg_vs2,0,0,0	script	Dc_Main_2	-1,{
	end;
OnMain:
	set 'Dc_timer,$@Dc_Delay['Dc_Truns];
	for (set @num,0;@num<$@Dc_Mob_Num_2['Dc_Truns];set @num,@num+1)
		monster "5@xg_vs2",50,49,"--ja--",$@Dc_Mob_Id_2[rand(getarraysize($@Dc_Mob_Id_2))],1,instance_npcname("Dc_Main_2")+"::OnKillMob";
	set 'Dc_Mob,$@Dc_Mob_Num_2['Dc_Truns];
	instance_announce instance_id(),"[ 中级道场 ] 魔物放出！第 "+'Dc_Truns+" 关挑战开始！",15;
	end;

OnKillMob:
	set 'Dc_Mob,'Dc_Mob-1;
	if('Dc_Mob>0)
		end;
	killmonsterall "5@xg_vs2";
	instance_announce instance_id(),"[ 中级道场 ] "+getpartyname('Dc_Party) +" 队伍, 第 "+'Dc_Truns+" 关挑战成功！",15;
	if ('Dc_Truns ==5) 
	{
	addtimer $@Dc_Time_Space_2*3000,instance_npcname("Dc_Main_2",instance_id(1))+"::OnGoTo";
	instance_announce instance_id(),"[ 中级道场 ] 您有15秒休息时间！",15;
	} else if ('Dc_Truns ==9)
	{
	addtimer $@Dc_Time_Space_2*3000,instance_npcname("Dc_Main_2",instance_id(1))+"::OnGoTo";
	instance_announce instance_id(),"[ 中级道场 ] 您有15秒休息时间！",15;
	} else {	
	addtimer $@Dc_Time_Space_2*1000,instance_npcname("Dc_Main_2",instance_id(1))+"::OnGoTo";
	}
	end;

OnGoTo:
	deltimer instance_npcname("Dc_Main_2",instance_id())+"::OnGoTo";
	if ('Dc_Truns==$@Dc_Misson_2)
	{
		instance_announce instance_id(),"[ 中级道场 ] "+getpartyname('Dc_Party) +" 队伍, 道场成功！",15;
		instance_announce instance_id(),"[ 中级道场 ] 请尽快领取奖品, 否则有可能领不到奖品！",15;
		instance_announce instance_id(),"[ 中级道场 ] 40秒后将背自动传送回首都",15;
		enablenpc instance_npcname("中级_奖品发放员",instance_id(1));
		instance_warpall "5@xg_vs2",49,47;
		sleep2 40000;
		instance_warpall "gonryun",159,118;
		instance_destroy(instance_id());	
	}
	else
	{
		set 'Dc_Truns,'Dc_Truns+1;
		doevent instance_npcname("Dc_Main_2",instance_id(1))+"::OnMain";
	}
	end;
}

5@xg_vs2,50,49,4	script	中级_裁判	758,{
	mes "[中级_裁判]";
	if ('Dc_Truns==0)
	{
		set 'Dc_Truns,1;
		set 'Dc_Party,getcharid(1);	
		set 'Dc_timer,$@Dc_Delay[0];
	}
	if (getpartyleader('Dc_Party,2)!=getcharid(0))
	{
		mes "只有队长才可以开始挑战";
		close;
	}
	if (getmapusers(has_instance("5@xg_vs2"))>12)
	{
		instance_warpall "prontera",156,108;
		set 'Dc_Party,0;
		close;
		end;
	}
	mes "您准备要开始挑战了吗？";
	menu "是的",-;
	close2;
	doevent instance_npcname("Dc_Main_2",instance_id(1))+"::OnMain";
	disablenpc instance_npcname("中级_裁判",instance_id(1));
	end;
}


5@xg_vs2,50,49,4	script	中级_奖品发放员	76,{ callfunc "Dc_Pay_2","奖品_中级"; }

function	script	Dc_Pay_2	{
	mes "["+getarg(0)+"发放员]";
	if (@Dc_Win_ZJ == 1 && instance_id(1))
	{
		mes "我是负责发放"+getarg(0)+"的";
		next;
		menu "我要领"+getarg(0)+"",-;
			set @Pay,rand(getarraysize($@Dc_Item_Id_2));
			getitem $@Dc_Item_Id_2[@Pay],$@Dc_Item_Num_2[@Pay];
			set zeny,zeny+$@Dc_Win_Zeny_2;
			set @Dc_Win_ZJ,0;
			dispbottom "[ROZ提示] -本次获取< "+$@Dc_Win_Zeny_2+" > Z. ";
	set zhongji,zhongji+1;
	dispbottom "[积分提示] -本次获取中级道场积分1点";
			mes "请等待被传送出去！！";
		}
	mes "请等待被传送出去！！";
	close;
}